apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

repositories {
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootProject.kotlinVersion"
        classpath 'io.fabric.tools:gradle:1.+'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("app/keystore/keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    ext {
        APP_NAME = "@string/app_name_default"
        APP_NAME_DEBUG = "@string/app_name_debug"
    }

    defaultConfig {
        applicationId "br.com.amaki.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1000
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "boolean", "REPORT_CRASH", "true"
            manifestPlaceholders = [appName: APP_NAME]
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            buildConfigField "boolean", "REPORT_CRASH", "false"
            manifestPlaceholders = [appName: APP_NAME_DEBUG]
        }
    }

    productFlavors {
        amakiADM {
            applicationId "br.com.amaki.adm"
            resValue "string", "app_name_default", "Amaki ADM"
            resValue "string", "app_name_debug", "Amaki ADM-Debug"
            buildConfigField "boolean", "IS_ADM", "true"
        }

        amakiCustomer {
            applicationId "br.com.amaki.app"
            resValue "string", "app_name_default", "Amaki"
            resValue "string", "app_name_debug", "Amaki-Debug"
            buildConfigField "boolean", "IS_ADM", "false"
        }
    }

    flavorDimensions "app"

    buildToolsVersion '28.0.3'
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //SUPPORT
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //TESTS
    testImplementation 'junit:junit:4.12'
    //RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.anko:anko:$ankoVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    //RX
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    //GLIDE
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "jp.wasabeef:glide-transformations:$glidetransfVersion"
    //FIREBASE
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"
    //FABRIC
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }

//    //VIEW PAGER INDICATOR
//    implementation 'com.github.JakeWharton:ViewPagerIndicator:2.4.1@aar'
//    //MULTIDEX
    implementation 'com.android.support:multidex:1.0.3'
//    //GMS
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
//    implementation "com.google.android.gms:play-services-places:$playServicesVersion"
//    implementation 'com.google.android.libraries.places:1.1.0'
    implementation 'com.google.android.libraries.places:places:1.1.0'
}

apply plugin: 'kotlin-android-extensions'
